000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.    FBK1P0M2.
000300*VSCOBII85
000400 AUTHOR.        FEP NEW CLAIMS - CGE&Y.
000500
000600******************************************************************
000700* FUNCTION:  PROCESS NEWCLAIM
000800******************************************************************
000900*
001000* CMR    DATE       REMARKS
001100******************************************************************
001200* 362991 NOV-02     NEW FEPDIRECT IMPLEMENTATION
001300*
001400* 369169 DEC-02     YEAR END CHANGES BY VIJAY MAGANTI  IN PARA
001500*                   4400-NEWCL-CONTR-ADDR FOR RETURNING BAD
001600*                   ADDRESSES INSTEAD OF JUST INDICATING THAT THE
001700*                   ADDRESS IS BAD
001800* 370189 JAN-03     RECATALOG FOR YEAR END CHANGES
001900* 351479 MAR-03     HIPAA PRIVACY CHANGES
002000* 354750 JUL-03     HIPAA CHANGES
002100* 371743 APR-04     INCORPORATE REAL TIME MODIFICATIONS.
002200* 407249 JAN-2006   UPDATE THE PROVIDER ROSTER LENGTH FOR
002300*                   COPY BOOK FWG07B66
002400* 407939 JAN-2007   UB04/DENTAL PROJECT AND YEAR END 2006 CHANGES
002500* 407249 JAN-2007   NPI PROJECT MOVE PROVIDER VSAM FILE TO DB2
002600* 69725  AUG-07     RECOMPILE DUE TO CHANGES IN COPYBOOK FWG07B01
002700* 98673  JAN-09     MODIFY FOR OBRA90.
002800* 254371 JUL-2010   RECOMPILE TO PICKUP TWR CHANGES    IO
      *TT361684 10/06/12  RECOMPILE FOR OTR RECORD CHANGE
002900*TT283828 06/25/11  HIPAA 5010 PROJECT - EXPAND NAME FIELDS
      *TT422847 10/06/12 TYPE-OF-BILL '*81*' WAS OVERRIDE BY SYSTEM
003000*TT373326 04/14/12  2011SOW20 - EXPAND CHARGE LINE FROM 40 TO 999
      *         01/01/13  2012SOW36 - CONTAINER CHANGE FOR OTR RECORD
      * 582897  07/13/13  FIX ABENDS DUE TO NO ADDRESSABILITY TO AREA
      * 605659  01/01/14  RECOMPILE TO ADDED BLUEDENTAL ID TO TWR       00009010
      * 667969  07/12/14  2014SOW030 MEDICARE AUDIT REMEDIATION PROJECT
      *                   RECOMPILE FOR EXPENDED OF OTR
      *  701486 01/01/15 2014SOW030 MEDICARE AUDIT REMEDIATION PROJECT
      *                  R4-2014 PHASE 2 CHANGES
      *  685200 01/01/15 2014SOW025 PROVIDER DATA ENHANCEMENT PROJECT
      *                  ADDED PERFORMING PROVIDER ADDRESS DATA TO OTR
      *  728744 04/25/15 2014SOW002 - ICD-10 BP FAILSAFE REMOVAL
      *  103496 01/01/16 2015R4 PRICING/PERCENTAGE FIELD TRUNCATION
      *                  RECOMPILE
      * 1165079 09/14/18 SEAMLESS ADJUDICATION CODE CLEAN UP
003100******************************************************************
003200 DATA DIVISION.
003300 WORKING-STORAGE SECTION.
003400 77  PAN-VALET PICTURE X(24) VALUE '001FBK1P0M2  05/19/04'.
003500
003600 01  CICS-FIELDS.
003700     05  CICS-TIME            COMP-3 PIC S9(15)  VALUE +0.
003800
003900 01  SUBSCRIPTS.
004000     05  SUB                         PIC 9(02)   VALUE ZEROES.
004100     05  ADR-SUB                     PIC 9(02)   VALUE ZEROES.
004500     05  FRM-SUB                COMP PIC 9(07)   VALUE 1.
004600     05  MSG-SUB                COMP PIC 9(07)   VALUE ZERO.
004700
004800 01  COUNTERS.
004900     05  ADD-LEN                COMP PIC S9(07)  VALUE +0.
005000     05  OPTION-COUNT                PIC 9(02)   VALUE  ZEROES.
005100     05  WS-NUMBER-OF-CHARGES        PIC 9(03)   VALUE  ZEROES.
005200
005300 01  DATES.
005400     05  MMDDCCYY-DATE               PIC X(08)   VALUE SPACES.
005500
005600 01  WS-BRE-SWITCHES.
005700     05  V-SUB-SW                       PIC X VALUE 'N'.
005800     05  C-SUB-SW                       PIC X VALUE 'N'.
005900     05  WS-PREV-CALL-TO-BRE-REPAIR-SW  PIC X.
006000
006100 01  CALLED-MODULES.
006400     05  FBK0P0IV-MODULE             PIC X(08)   VALUE 'FBK0P0IV'.
006700     05  FX71PIII-MODULE             PIC X(08)   VALUE 'FX71PIII'.
006800     05  FX71P0IV-MODULE             PIC X(08)   VALUE 'FX71P0IV'.
007900
008000 01  MESSAGES.
008100     05  RECORD-NOT-FOUND-MSG               PIC X(30)   VALUE
008200        'RECORD NOT FOUND              '.
009300
       01  WORK-FIELDS.
           05  WS-NUM-OF-CHG-LINES        COMP  PIC S9(07).
025680     05  WS-MSG-LENGTH                    PIC 9(7).
032300*    05  MSG-LENGTH                       PIC S9(8) COMP.
041006     05  IN-OTR-PARMS-VLEN                PIC 9(09) VALUE ZEROES. 00005300
036400     05  ADR-SWITCH                       PIC X.
SOW36  01  ADD-MSG.
SOW36      05  ADD-MSG-CHAR         PIC X OCCURS 1000 TIMES.
029200
010300*-----------------------------------------------------------------
010400*--- FBK0P0IV PARAMETERS
010500*-----------------------------------------------------------------
010600 01  FBK0P0IV-PARMS.
010700     05  FBK0P0IV-FUNCTION                  PIC X.
010800         88  FBK0P0IV-VALIDATE-CONTRACT-ID       VALUE '0'.
010900         88  FBK0P0IV-GET-ADDRESS-REC            VALUE '2'.
011000         88  FBK0P0IV-PATIENT-ROSTER             VALUE '3'.
011100         88  FBK0P0IV-HIPAA-PRIVACY              VALUE '6'.
011200     05  FBK0P0IV-CONTRACT-ID               PIC 9(08).
011300     05  FBK0P0IV-PLAN-CODE                 PIC 9(03).
011400     05  FBK0P0IV-PATIENT-CODE              PIC X.
011500
011600     05  FBK0P0IV-MESSAGE-AREA.
011700         10  FBK0P0IV-RESPONSE              PIC X.
011800             88  FBK0P0IV-ERROR                  VALUE 'E'.
011900             88  FBK0P0IV-FAILED                 VALUE 'N' '0'.
012000             88  FBK0P0IV-SUCCESS                VALUE 'Y'.
012100         10  FBK0P0IV-MESSAGE               PIC X(30).
013500
019000 01  COMMAREAS.
020100*
020300     COPY FX71CCII.
020400*
020500     COPY FX71CCIV.
026600*------------------------------------------------------------
026700*    OC ONLINE SEND PARAMETER AREAS
026800*------------------------------------------------------------
026900     COPY FWG07B65.
028500*
028600*    FX71P0ME DATA AREA ERROR LOG
028700*
028800 COPY FWG07792.
028900
006006 01  IN-FBK0PCM0-OPTIONS.
007006     05  IN-FBK0PCM0-OPTION      PIC X OCCURS 20 TIMES.
034200
018400 01  WS-CONTAINER-FIELDS.
025620     05  WS-LOW-VALUE               PIC X      VALUE LOW-VALUES.
025670     05  WS-COMMAND-RESP            PIC S9(8)  COMP.
025680     05  WS-COMMAND-RESP2           PIC S9(8)  COMP.

025620 01  WS-FIRST-BYTE-RESP         PIC X  VALUE 'S'.
      *
      *======================
      * OTR RECORD LENGTH
      *======================
           COPY FBK1CC01.
      *

017900     COPY FKMQCC01.

034300 LINKAGE SECTION.
034400 01  PARM-RC                                 PIC 9(02).
034700 01  PARM-LEN                                PIC 9(07).
034800 01  PARM-CLAIM-ID-TBL.
034900     05  PARM-CLAIM-ID      OCCURS 201 TIMES PIC X(09).
035000 01  PARM-CLAIM-TYPE-IND                     PIC X.
035100 01  PARM-REAL-TIME-IND                      PIC X.
035200
035300/
       01 LS-OTR-CONT-SW            PIC X(01).
       01 LS-OTR-MESSAGE            PIC X(2153).
080006 01  IN-NEWCLAIMS             REDEFINES LS-OTR-MESSAGE.
005006     05                              PIC X(08).
006006     05  IN-NEWCL-OPTIONS.
007006         10 IN-NEWCL-OPTION          PIC X OCCURS 20 TIMES.
10000      05  IN-NEWCL-PLAN-CODE          PIC X(03).
11000      05  IN-NEWCL-CONTRACT-ID        PIC X(08).
120006     05  IN-NEWCL-PATIENT-CODE       PIC X(01).
130006     05  IN-NEWCL-CLAIM-NUM          PIC X(14).
910006 01  IN-OTR-SINGLE-OPT        REDEFINES LS-OTR-MESSAGE.
005006     05                              PIC X(08).
006006     05  IN-OTRS-OPTIONS.
007006         10 IN-OTRS-OPTION           PIC X OCCURS 20 TIMES.
930006     05  IN-OTRS-CONTRACT-ID         PIC X(08).
940006     05  IN-OTRS-BIRTH-DATE          PIC X(08).
950007     05  IN-OTRS-FIRST-NM            PIC X(35).
960006     05  IN-OTRS-MI-NM               PIC X.
970006     05  IN-OTRS-GENDER              PIC X.
980007     05  IN-OTRS-LAST-NM             PIC X(60).
990006     05  IN-OTRS-SUFFIX-NM           PIC X(05).
000006     05  IN-OTRS-PATIENT-CODE        PIC X.
010006     05  IN-OTRS-STATUS              PIC X(6).
021007     05                              PIC X(2001).
      *
035500
       01  IN-OTR-CONTAINER-PARM.
           04  IN-OTR-CHANNEL-NAME     PIC X(16).
           04  IN-OTR-CONTAINER-NAME   PIC X(16).
           04  IN-OTR-CONTAINER-LENGTH PIC S9(09) COMP VALUE 0.
           04  IN-OTR-CONTAINER-PTR    USAGE IS POINTER.
           04  IN-OTR-CONTAINER-ADDR   REDEFINES IN-OTR-CONTAINER-PTR
                                    PIC 9(08) COMP.

       01  OUT-OTR-CONTAINER-PARM.
           04  OUT-OTR-CHANNEL-NAME    PIC X(16).
           04  OUT-OTR-CONTAINER-NAME  PIC X(16).
           04  OUT-OTR-CONTAINER-LENGTH PIC S9(09) COMP VALUE 0.
           04  OUT-OTR-CONTAINER-PTR   USAGE IS POINTER.
           04  OUT-OTR-CONTAINER-ADDR  REDEFINES OUT-OTR-CONTAINER-PTR
                                    PIC 9(08) COMP.

820006*01 IN-OTR-PARMS.
           COPY FBK1CC02.
037700*
SOW360*01  OUT-PARMS.
028400     COPY FWG07B07.
SOW360***  02  OUT-RESPONSE                PIC X.
SOW360*    02  OUT-MESSAGE.
SOW36 *        10  OUT-MESSAGE-CHAR        PIC X OCCURS 1000000 TIMES
SOW36 *                        DEPENDING ON OUT-OTR-CONTAINER-LENGTH.
037900/
038000 PROCEDURE DIVISION USING PARM-RC
038100                          PARM-LEN
038200                          PARM-CLAIM-ID-TBL
038300                          PARM-CLAIM-TYPE-IND
038400                          PARM-REAL-TIME-IND
                                LS-OTR-CONT-SW
                                LS-OTR-MESSAGE
038500                          IN-OTR-CONTAINER-PARM
038500                          OUT-OTR-CONTAINER-PARM.
038700
038800     PERFORM 1000-INITIALIZATION
038900     PERFORM 4000-PROCESS-NEWCLAIM
039000     GO TO 9999-GOBACK
039100     .
039200/
039300 1000-INITIALIZATION.

040500     MOVE ZERO                 TO MSG-SUB SUB
040700                                  ADR-SUB
041200     MOVE LS-OTR-MESSAGE(9:28) TO IN-FBK0PCM0-OPTIONS
041500
SOW36      PERFORM  9900-SETUP-OTR-CONTAINER THRU 9900-EXIT
048600     MOVE 'S'                  TO ADD-MSG
048700     MOVE 1                    TO ADD-LEN
SOW36      PERFORM  U970-ADD-MESSAGE THRU U970-EXIT
041400     .
041600/
041700 4000-PROCESS-NEWCLAIM.
041800
041900     PERFORM  U997-GET-OPTION-COUNT
043000
043100     PERFORM  VARYING  SUB   FROM  1  BY  1  UNTIL SUB  >  2
043200        EVALUATE  IN-FBK0PCM0-OPTION (SUB)
043900          WHEN '4' PERFORM 4500-PATNT-ROSTER
045100          WHEN OTHER   GO TO 9999-GOBACK
045200        END-EVALUATE
045300     END-PERFORM
045400     .
045600/
062800*=================================================================
062900*===  PATIENT ROSTER PROCESS
063000*=================================================================
063100 4500-PATNT-ROSTER.
063200
063300     SET   FBK0P0IV-PATIENT-ROSTER  TO TRUE.
063400     MOVE  IN-NEWCL-CONTRACT-ID     TO FBK0P0IV-CONTRACT-ID.
063500
063600     PERFORM  U200-CALL-ENROLLMENT
063700
063800     EVALUATE TRUE
063900         WHEN FBK0P0IV-ERROR
064000              MOVE  LENGTH OF FBK0P0IV-MESSAGE-AREA  TO  PARM-LEN
064100              MOVE  FBK0P0IV-MESSAGE-AREA            TO  OUT-PARMS
064200              GO TO 9999-GOBACK
064300         WHEN FBK0P0IV-FAILED
064400              MOVE  LENGTH OF
064500                    FBK0P0IV-MESSAGE-AREA  TO  ADD-LEN
064600              MOVE  FBK0P0IV-MESSAGE-AREA  TO  ADD-MSG
064700              PERFORM  U970-ADD-MESSAGE THRU U970-EXIT
064800         WHEN FBK0P0IV-SUCCESS
064900              COMPUTE  ADD-LEN  = 2
065000                       + (FWG07B65-NO-CT-PATIENT-N
065100                       * LENGTH OF FWG07B65-PAT-ROSTER-ELEM)
                    COMPUTE MSG-SUB   = PARM-LEN + 1
065200              MOVE  FWG07B65-FUNCCODE-L    TO
                                      OUT-MESSAGE (MSG-SUB:ADD-LEN)
                    COMPUTE PARM-LEN  = PARM-LEN + ADD-LEN
065400     END-EVALUATE
065500     .
065600
065700/
141800 U200-CALL-ENROLLMENT.
141900
142000     CALL FBK0P0IV-MODULE USING DFHEIBLK
142100                                DFHCOMMAREA
142200                                FBK0P0IV-PARMS
142300                                FWG07B65-PARMS
142400     END-CALL
142500     .
142800/
151300 U970-ADD-MESSAGE.
151400*=================================================================
151500*===  ADD MESSAGE TO THE COMMON MESSAGES AREA.
151600*===  PARM-LEN POINTS TO THE LAST USED CHAR IN THE COMMON MSG.
151700*=================================================================
151800
SOW36      COMPUTE MSG-SUB   = PARM-LEN + 1
151900     PERFORM  VARYING  FRM-SUB  FROM  1  BY  1
152000       UNTIL  PARM-LEN  >  IN-PARMS-MAX
152100          OR  FRM-SUB   >  ADD-LEN
152300         MOVE ADD-MSG-CHAR (FRM-SUB) TO OUT-MESSAGE-CHAR (MSG-SUB)
SOW36          ADD   1       TO  MSG-SUB   PARM-LEN
152400     END-PERFORM
152500     .
152600 U970-EXIT.
152700     EXIT.
152800/
152900 U997-GET-OPTION-COUNT.
153000*=================================================================
153100*===  GET NUMBER OF REQUESTED PROCESSES
153200*=================================================================
153300
153400*-----------------------------------------------------------------
153500*--- COUNT THE NUMBER OF OPTIONS REQUESTED
153600*-----------------------------------------------------------------
153700     PERFORM VARYING SUB FROM 1 BY 1
153800       UNTIL SUB > 2  OR IN-FBK0PCM0-OPTION (SUB) NOT > SPACES
153900     END-PERFORM
154000
154100     COMPUTE OPTION-COUNT = SUB - 1
154200     .
154300
154400/
154500 U998-GET-DATE.
154600
154700     EXEC CICS  ASKTIME
154800                ABSTIME (CICS-TIME)
154900                END-EXEC.
155000
155100     EXEC CICS  FORMATTIME
155200                ABSTIME (CICS-TIME)
155300                MMDDYYYY(MMDDCCYY-DATE)
155400     END-EXEC
155500     .
155600
155700/
046000*=================================================================
046100*==  MQ/CONTAINER ERROR LOG AND HANDLING
046200*=================================================================
046300     COPY FKMQCC99.
046300
046500/
044900 9900-SETUP-OTR-CONTAINER.

SOW36      MOVE  62000          TO  WS-MSG-LENGTH
           MOVE  WS-MSG-LENGTH  TO OUT-OTR-CONTAINER-LENGTH.

           INITIALIZE              FKMQMESG-PARMS.

           EXEC CICS PUT CONTAINER(OUT-OTR-CONTAINER-NAME)
                         CHANNEL(OUT-OTR-CHANNEL-NAME)
                         FROM(WS-LOW-VALUE)
                         FLENGTH(OUT-OTR-CONTAINER-LENGTH)
                         RESP(WS-COMMAND-RESP)
                         RESP2(WS-COMMAND-RESP2)
           END-EXEC

           INITIALIZE              FX71P0ME-PARMS.
           PERFORM  T999-CHECK-CONTAINER-ERROR  THRU T999-EXIT
           IF FKMQMESG-RET-ERROR
               MOVE    'FBK102'                 TO  FX71P0ME-PGM-PREFIX
               MOVE     FKMQMESG-ERR-PUT        TO  FX71P0ME-MSG-NO
               PERFORM  U999-LOG-ERROR        THRU  U999-EXIT
           END-IF.

           EXEC CICS GET CONTAINER(OUT-OTR-CONTAINER-NAME)
                         CHANNEL(OUT-OTR-CHANNEL-NAME)
                         SET(OUT-OTR-CONTAINER-PTR)
                         FLENGTH(OUT-OTR-CONTAINER-LENGTH)
                         RESP(WS-COMMAND-RESP)
                         RESP2(WS-COMMAND-RESP2)
           END-EXEC

           INITIALIZE              FX71P0ME-PARMS
           PERFORM  T999-CHECK-CONTAINER-ERROR  THRU T999-EXIT
           IF FKMQMESG-RET-ERROR
               MOVE    'FBK102'                 TO  FX71P0ME-PGM-PREFIX
               MOVE     FKMQMESG-ERR-GET        TO  FX71P0ME-MSG-NO
               PERFORM  U999-LOG-ERROR        THRU  U999-EXIT
           END-IF.

           SET ADDRESS OF OUT-PARMS     TO OUT-OTR-CONTAINER-PTR.
           MOVE LOW-VALUES TO
                          OUT-PARMS(1:OUT-OTR-CONTAINER-LENGTH).

020100 9900-EXIT.  EXIT.
046600/
155800 9999-GOBACK.
155900
156100     GOBACK
156200     .
156400*
156500*    FX71P0ME MODULE CALL - ERROR LOG
156600*
156700     COPY FWG07793.
